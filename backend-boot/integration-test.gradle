configurations {
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/test-integration/java')
    }

    resources.srcDir file('src/test-integration/resources')
  }
}

task integrationTest(type: Test) {
  description = 'Runs the backend integration tests.'
  group = 'verification'
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  outputs.upToDateWhen { false }
  mustRunAfter test

  useJUnitPlatform()

  testLogging {
    // events = ['FAILED', 'PASSED', 'SKIPPED', 'STANDARD_OUT']
    events = ['FAILED', 'PASSED', 'SKIPPED']
  }

  doFirst {
    file("$rootDir/.env").readLines().each() {
      def (key, value) = it.tokenize('=')
      environment key, value
    }
  }
}

check {
  dependsOn integrationTest
}
