plugins {
  id 'java'

  id 'org.springframework.boot' version '2.3.0.RELEASE'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'

  /* PIT Mutation Testing */
  id 'info.solidsoft.pitest' version '1.5.1'
}

/* Integration Tests */
apply from: "$rootDir/backend-boot/integration-test.gradle"

java {
  sourceCompatibility = JavaVersion.VERSION_14
  targetCompatibility = JavaVersion.VERSION_14
}

repositories {
  mavenCentral()
  jcenter()
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

sourceSets {
  main {
    resources {
      srcDir 'generated/main/resources'
    }
  }
}

dependencies {
  /* Lombok */
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  /* Spring */
  implementation 'org.springframework.boot:spring-boot-starter-web'
  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  /* Health and Info endpoints */
  implementation 'org.springframework.boot:spring-boot-starter-actuator'

  /* Guava/SHA-256 */
  testImplementation 'com.google.guava:guava:29.0-jre'

  /* Data */
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.flywaydb:flyway-core:6.4.3'
  implementation 'org.postgresql:postgresql:42.2.12'

  /* Spring/OpenApi */
  implementation 'org.springdoc:springdoc-openapi-ui:1.3.9'
}

test {
  useJUnitPlatform()
  testLogging {
    // events = ['FAILED', 'PASSED', 'SKIPPED', 'STANDARD_OUT']
    events = ['FAILED', 'PASSED', 'SKIPPED']
  }
}

bootRun {
  doFirst {
    file("$rootDir/.env").readLines().each() {
      def (key, value) = it.tokenize('=')
      environment key, value
    }
  }
}

pitest {
  targetClasses = ['demo.games.*']
  timestampedReports = false
  junit5PluginVersion = '0.12'
}

build {
  dependsOn 'pitest'
}
